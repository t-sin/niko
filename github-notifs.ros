#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp (ql:quickload '(:dexador :jonathan :cl-ppcre) :silent t)
  )

(defpackage :ros.script.github-notifs.3732696253
  (:use :cl))
(in-package :ros.script.github-notifs.3732696253)

(defparameter *github-token* "your tokens")

(defun get-notifications (token &optional since all)
  (handler-bind
      ((error
        (lambda (c)
          (format t "~s~%" c)
          (return-from get-notifications))))
    (multiple-value-bind (res status header uri ssl)
        (let ((uri (format nil "~a?~a"
                           "https://api.github.com/notifications"
                           (quri:url-encode-params `(,(if since
                                                          `("since" . ,since)
                                                          '("since" . ""))
                                                      ,(if all
                                                           `("all" . ,all)
                                                           '("all" . "false")))))))
          (format t "~s~%" uri)
          (dex:get uri
                   :headers `(("Authorization" . ,(concatenate 'string "token " token)))))
      (declare (ignore status ssl uri))
      (values
       (jojo:parse res)
       (gethash "last-modified" header)
       (gethash "x-poll-interval" header)))))

(defun get-issue (token owner repo id)
  (handler-bind
      ((error
         (lambda (c)
           (format t "~s~%" c))))
    (multiple-value-bind (res status header uri ssl)
        (let ((uri (format nil "~a/repos/~a/~a/issues/~a"
                           "https://api.github.com"
                           owner repo id)))
          (format t "~s~%" uri)
          (dex:get uri :headers `(("Authorization" . ,(concatenate 'string "token " token)))))
      (declare (ignore header status ssl uri))
      (values
       (jojo:parse res)))))

(defun get-issue-comment (token owner repo id)
  (handler-bind
      ((error
         (lambda (c)
           (format t "~s~%" c))))
    (multiple-value-bind (res status header uri ssl)
        (let ((uri (format nil "~a/repos/~a/~a/issues/comments/~a"
                           "https://api.github.com"
                           owner repo id)))
          (format t "~s~%" uri)
          (dex:get uri :headers `(("Authorization" . ,(concatenate 'string "token " token)))))
      (declare (ignore header status ssl uri))
      (values
       (jojo:parse res)))))

;;; 所属するチーム全てのメンションを知りたい
;;; スレッド購読して、新着のメッセージを解析か。
(defun get-mentioned (token &optional since all)
  (multiple-value-bind (notifications last-modified poll-interval)
      (get-notifications token since all)
    (format t "~s~%" (list last-modified poll-interval))
    (loop
       :for n :in notifications
       :when (member (getf n :|reason|) '("author"  ; 自分がauthorのスレッドにコメントきたから
                                          "comment"  ; 自分がスレッドにコメントしたから
                                          "mention"  ; 明示的なメンションされたから
                                          "assign"  ; 割り当てされたとき
                                          "subscribed")  ; スレッド購読してたから
                     :test #'string=)
       :collect (list (getf n :|subject|)
                      (intern (getf n :|reason|) :keyword)))))

(defun parse-subject (subject)
  (let ((issue (multiple-value-bind (match strs)
                   (ppcre:scan-to-strings "/repos/([^/]+)/([^/]+)/issues/(.+$)"
                                          (getf subject :|url|))
                 (when match
                   (let ((owner (aref strs 0))
                         (repo (aref strs 1))
                         (issue-id (aref strs 2)))
                     (list owner repo issue-id)))))
        (comment-id (multiple-value-bind (match strs)
                        (ppcre:scan-to-strings "/repos/[^/]+/[^/]+/issues/comments/(.+$)"
                                               (getf subject :|latest_comment_url|))
                      (when match
                        (aref strs 0)))))
    (values issue comment-id)))


(defun issue-body (token subject)
  (multiple-value-bind (issue comment-id)
      (parse-subject subject)
    (destructuring-bind (owner repo issue-id) issue
      (getf (if comment-id
                (get-issue-comment token owner repo comment-id)
                (get-issue token owner repo issue-id))
            :|body|))))

(defun all-mentions-from (text)
  (ppcre:all-matches-as-strings "@[^ ]+" text))

(defun all-mentions (token nots)
  (remove-duplicates
   (loop
     :for n :in nots
     :nconc (all-mentions-from (issue-body token (first n))))
   :test #'string=))

(defun main (&rest argv)
  (declare (ignorable argv))
  (format t "~s~%" argv)
  (let* ((token (first argv))
         (since (second argv))
         (all (third argv))
         (mentioned (get-mentioned token since all)))
    (format t "mentioned: ~s~%" (all-mentions token mentioned))))
;;; vim: set ft=lisp lisp:
